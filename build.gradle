group 'github.com.dhunziker'
version '1.0-SNAPSHOT'

apply plugin: 'java'

sourceCompatibility = 1.8

configurations {
    provided
    compile.extendsFrom provided
}

repositories {
    mavenCentral()
}

dependencies {
    provided group: 'org.apache.jmeter', name: 'ApacheJMeter_core', version: '3.0'
    compile group: 'org.freemarker', name: 'freemarker', version: '2.3.23'
    testCompile group: 'junit', name: 'junit', version: '4.11'
    testCompile group: 'org.mockito', name: 'mockito-all', version: '1.10.19'
}

task fatJar(type: Jar) {
    dependsOn configurations.runtime
    manifest {
        attributes 'Implementation-Title': project.name, 'Implementation-Version': version
    }
    baseName = project.name + '-all'

    from {
        (configurations.runtime - configurations.provided).collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }

    with jar
}

task installJar(type: Copy, dependsOn: fatJar) {
    if (System.env.JMETER_HOME == null) {
        throw new GradleException('Required JMETER_HOME environment variable not set.')
    }

    from(libsDir) {
        include '**/*-all*.jar'
    }
    into "${System.env.JMETER_HOME}/lib/ext"
}
